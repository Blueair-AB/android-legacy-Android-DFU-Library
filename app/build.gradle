plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}
apply from: rootProject.file("gradle/git-tag-version.gradle")

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Release")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}

android {
    namespace 'no.nordicsemi.android.dfu.app'
    compileSdk android_api_version

    defaultConfig {
        applicationId "no.nordicsemi.android.dfu"
        minSdk android_min_api_version
        targetSdk android_api_version
        versionCode getVersionCodeFromTags()
        versionName getVersionNameFromTags()
    }

    signingConfigs {
        release {
            storeFile file('../keystore')
            storePassword System.env.KEYSTORE_PSWD
            keyAlias System.env.KEYSTORE_ALIAS
            keyPassword System.env.KEYSTORE_KEY_PSWD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    buildFeatures {
        compose true
    }
}
hilt {
    enableAggregatingTask = true
}
kapt {
    correctErrorTypes true
}

dependencies {
    implementation project(":lib:analytics")
    implementation project(":lib:storage") // Deep link support
    implementation project(":profile:navigation")

    implementation "no.nordicsemi.android.common:theme:$nordic_common_version"
    implementation "no.nordicsemi.android.common:navigation:$nordic_common_version"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:$material3_version"
    implementation "androidx.activity:activity-compose:1.6.0"

    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_version"
}
